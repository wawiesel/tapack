!!# FUNCTION MODULE: <FUN_EXIST>
MODULE FUN_EXIST

!!## PURPOSE
!! The function checks if a file EXISTs and is defined in the
!! exact location specified by <FILE>.


!!## USAGE
!
!  L = EXIST( FILE )
!
!! where <L> is a <LOGICAL> and <FILE> is a filename of regular
!! string type, <CHARACTER>, or the variable-length
!! string type, <varying_string>.


!!## EXTERNAL KINDS
USE KND_IntrinsicTypes,ONLY: KIND_S !!((01-A-KND_IntrinsicTypes.f90))

!!## FORTRAN STANDARDS
USE ISO_varying_string              !!((03-A-ISO_varying_string.f90))

!!## DEFAULT IMPLICIT
IMPLICIT NONE

!!## DEFAULT ACCESS
PRIVATE

!!## PROCEDURE OVERLOADING
INTERFACE EXIST
 MODULE PROCEDURE EXIST_S0
 MODULE PROCEDURE EXIST_VS0
 MODULE PROCEDURE EXIST_S1
 MODULE PROCEDURE EXIST_VS1
END INTERFACE

!!## PUBLIC ACCESS LIST
PUBLIC :: EXIST


!!## CONTAINED PROCEDURES
CONTAINS


!!### FUNCTION: EXIST_S0
FUNCTION EXIST_S0(FILE) RESULT(EXIST)
!!#### REQUIRED INPUT
CHARACTER(LEN=*,KIND=KIND_S),INTENT(IN) :: FILE
!!#### REQUIRED OUTPUT
LOGICAL :: EXIST
!!--begin--
INQUIRE(FILE=FILE,EXIST=EXIST)
!!--end--
END FUNCTION

!!### FUNCTION: EXIST_VS0
FUNCTION EXIST_VS0(FILE) RESULT(EXIST)
!!#### REQUIRED INPUT
TYPE(varying_string),INTENT(IN) :: FILE
!!#### REQUIRED OUTPUT
LOGICAL :: EXIST
!!--begin--
INQUIRE(FILE=char(FILE),EXIST=EXIST)
!!--end--
END FUNCTION

!!### FUNCTION: EXIST_S1
FUNCTION EXIST_S1(FILE) RESULT(EXIST)
!!#### REQUIRED INPUT
CHARACTER(LEN=*,KIND=KIND_S),INTENT(IN) :: FILE(:)
!!#### REQUIRED OUTPUT
LOGICAL :: EXIST(1:SIZE(FILE))
!!#### LOCAL VARIABLES
INTEGER :: n
!!--begin--
DO n=1,SIZE(FILE)
 INQUIRE(FILE=TRIM(FILE(n)),EXIST=EXIST(n))
END DO
!!--end--
END FUNCTION

!!### FUNCTION: EXIST_VS1
FUNCTION EXIST_VS1(FILE) RESULT(EXIST)
!!#### REQUIRED INPUT
TYPE(varying_string),INTENT(IN) :: FILE(:)
!!#### REQUIRED OUTPUT
LOGICAL :: EXIST(1:SIZE(FILE))
!!#### LOCAL VARIABLES
INTEGER :: n
TYPE(varying_string) :: VS
!!--begin--
DO n=1,SIZE(FILE)
 VS = TRIM(FILE(n))
 INQUIRE(FILE=char(VS),EXIST=EXIST(n))
END DO
!!--end--
END FUNCTION


END MODULE
